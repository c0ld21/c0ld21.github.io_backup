---
title: Decompetition CTF - leipzig
date: 2022-02-12
categories: [re]
tags: [decompetition2022]
toc: false
---

#### Problem (leipzig):
- This code revolves around using functions in setjmp.h and sending various signals. From initial observation, it performs various inc and dec operations on global variables.


#### main():
```
main:
  endbr64
  push    rbp
  mov     rbp, rsp
  sub     rsp, 0x20
  mov     [rbp-0x14], edi
  mov     [rbp-0x20], rsi
  cmp     [rbp-0x14], 2             ; check if argc != 2
  je      block2
block1:                             ; raise(6) & exit if argc != 2
  mov     rax, [stderr]
  mov     rcx, rax
  mov     edx, 6
  mov     esi, 1
  lea     rdi, [mem1]; "Nein!\n"
  call    fwrite@plt.sec
  mov     edi, 6
  call    raise@plt.sec
block2:                             ; set global COUNTER to 0
  mov     [COUNTER], 0
  mov     rax, [rbp-0x20]
  add     rax, 8
  mov     rax, [rax]
  mov     rdi, rax
  call    atoi@plt.sec              ; set global CURRENT to int val via atoi(argv[1])
  cdqe
  mov     [CURRENT], rax
  mov     rax, [CURRENT]
  test    rax, rax
  jg      block4                    ; if CURRENT < 1, raise(6) and exit, otherwise
block3:                               go to block 4
  mov     rax, [stderr]
  mov     rcx, rax
  mov     edx, 8
  mov     esi, 1
  lea     rdi, [mem2]; "Nein...\n"
  call    fwrite@plt.sec
  mov     edi, 6
  call    raise@plt.sec
block4:                             ; send a bunch of signals in the following order:
  lea     rsi, [dec]                   signal(0xa, &dec)
  mov     edi, 0xa                     signal(0xc, &inc)
  call    signal@plt.sec               signal(0x15, &chk)
  lea     rsi, [inc]                   signal(0x16, &pty)
  mov     edi, 0xc
  call    signal@plt.sec
  lea     rsi, [chk]
  mov     edi, 0x15
  call    signal@plt.sec             
  lea     rsi, [pty]
  mov     edi, 0x16
  call    signal@plt.sec
  call    getpid@plt.sec
  mov     [rbp-8], eax              ; pid (rbp-0x8) = getpid(), pid is set to volatile
  mov     esi, 1                       in the C code so that it doesn't get clobbered
  lea     rdi, [RESTART]
  call    __sigsetjmp@plt.sec
  endbr64
  mov     [rbp-4], eax              ; sig (rbp-0x4) = sigsetjmp(RESTART, 0x1)
  cmp     [rbp-4], 0
  jne     block6
block5:
  mov     [rbp-4], 0x15             ; sig = 0x15, if sigsetjmp() is unsuccessful
block6: 
  mov     eax, [rbp-8]
  mov     edx, [rbp-4]
  mov     esi, edx
  mov     edi, eax
  call    kill@plt.sec              ; kill(pid, sig) and exit function
  mov     eax, 0
  leave
  ret
```

#### main() in C looks like:
```
int main(int argc, char *argv[]) {
  volatile pid_t pid;
  int sig;
  if (argc != 2) {
      fwrite("Nein!\n", 1, 6, stderr);
      raise(6);
  } 
  COUNTER = 0;
  CURRENT = atoi(argv[1]);
  if (CURRENT < 1) {
      fwrite("Nein...\n", 1, 8, stderr);
      raise(6);
  } 
  
  signal(0xa, &dec);
  signal(0xc, &inc);
  signal(0x15, &chk);
  signal(0x16, &pty);
  pid = getpid();
  sig = sigsetjmp(RESTART, 0x1);
  if (!sig) {
      sig = 0x15;
  }
  kill(pid, sig);
}
```

#### chk():
```
chk:
  endbr64
  push    rbp
  mov     rbp, rsp
  mov     rax, [CURRENT]
  cmp     rax, 1
  jg      block2                ; jump to block2 to call siglongjmp(RESTART, 0x16) 
block1:                           if global CURRENT > 2, otherwise print global COUNTER
  mov     rax, [COUNTER]          and flush stdout + raise(2)
  mov     rsi, rax
  lea     rdi, [mem1]; "%ld"
  mov     eax, 0
  call    printf@plt.sec
  mov     rax, [stdout]
  mov     rdi, rax
  call    fflush@plt.sec
  mov     edi, 2
  call    raise@plt.sec
block2:
  mov     esi, 0x16
  lea     rdi, [RESTART]
  call    siglongjmp@plt.sec
```

#### chk() in C looks like:
```
void chk() {
    if (CURRENT < 2) {
        printf("%ld", COUNTER);
        fflush(stdout);
        raise(2);
    }
    siglongjmp(RESTART, 0x16);
}
```

#### pty():
```
pty:                            ; checks global CURRENT & 1 to determine whether to call
  endbr64                         siglongjmp(RESTART, 0xc) or siglongjmp(RESTART, 0xa)
  push    rbp
  mov     rbp, rsp
  mov     rax, [CURRENT]
  and     eax, 1
  test    rax, rax
  je      block2
block1:
  mov     esi, 0xc
  lea     rdi, [RESTART]
  call    siglongjmp@plt.sec
block2:
  mov     esi, 0xa
  lea     rdi, [RESTART]
  call    siglongjmp@plt.sec
```

#### pty() in C looks like:
```
void pty() {
    if (CURRENT & 1) {
        siglongjmp(RESTART, 0xc);
    } else {
        siglongjmp(RESTART, 0xa);
    }
}
```

#### inc() and dec():
```
dec:
  endbr64
  push    rbp
  mov     rbp, rsp
  mov     rax, [COUNTER]
  add     rax, 1
  mov     [COUNTER], rax           ; increment global COUNTER by 1
  mov     rax, [CURRENT]           ; optimized division for CURRENT, this basically does
  mov     rdx, rax                   CURRENT /= 2
  shr     rdx, 0x3f
  add     rax, rdx
  sar     rax, 1
  mov     [CURRENT], rax
  mov     esi, 0x15                ; set up call to siglongjmp(RESTART, 0x15)
  lea     rdi, [RESTART]
  call    siglongjmp@plt.sec
inc:
  endbr64
  push    rbp
  mov     rbp, rsp
  mov     rax, [COUNTER]
  add     rax, 1
  mov     [COUNTER], rax           ; increment global COUNTER by 1
  mov     rdx, [CURRENT]
  mov     rax, rdx
  add     rax, rax
  add     rax, rdx
  mov     [CURRENT], rax           ; CURRENT += CURRENT * 2
  mov     rax, [CURRENT]
  add     rax, 1
  mov     [CURRENT], rax           ; CURRENT += 1
  mov     esi, 0x15
  lea     rdi, [RESTART]
  call    siglongjmp@plt.sec       ; siglongjmp(RESTART, 0x15);
```

#### inc() and dec() in C looks like:
```
void inc() {
    COUNTER += 1;
    CURRENT += CURRENT*2;
    CURRENT += 1;
    siglongjmp(RESTART, 0x15);
}

void dec() {
    COUNTER += 1;
    CURRENT /= 2;
    siglongjmp(RESTART, 0x15);
}
```

#### Summary:
- This program takes input from the command line and sets CURRENT to atoi(argv[1])
- setjmp saves and restores the set of blocked signals [More info](https://www.gnu.org/software/libc/manual/html_node/Non_002dLocal-Exits-and-Signals.html) 
- sigjmp_buf is similar to jmp_buf, but stores state info about the set of blocked signals
- all of the siglongjmp() serves to restore the stack env saved by sigsetjmp() [More info](https://www.ibm.com/docs/en/i/7.1?topic=ssw_ibm_i_71/apis/siglngj.htm)

#### Full Code:
```
#include <stdio.h>
#include <stdlib.h>
#include <setjmp.h>
#include <signal.h>
#include <unistd.h>

void chk();
void pty();
void inc();
void dec();

long CURRENT = 0;
size_t COUNTER = 0;
sigjmp_buf RESTART;

int main(int argc, char *argv[]) {
  volatile pid_t pid;
  int sig;
  if (argc != 2) {
      fwrite("Nein!\n", 1, 6, stderr);
      raise(6);
  } 
  COUNTER = 0;
  CURRENT = atoi(argv[1]);
  if (CURRENT < 1) {
      fwrite("Nein...\n", 1, 8, stderr);
      raise(6);
  } 
  
  signal(0xa, &dec);
  signal(0xc, &inc);
  signal(0x15, &chk);
  signal(0x16, &pty);
  pid = getpid();
  sig = sigsetjmp(RESTART, 0x1);
  if (!sig) {
      sig = 0x15;
  }
  kill(pid, sig);
}

void chk() {
    if (CURRENT < 2) {
        printf("%ld", COUNTER);
        fflush(stdout);
        raise(2);
    }
    siglongjmp(RESTART, 0x16);
}

void pty() {
    if (CURRENT & 1) {
        siglongjmp(RESTART, 0xc);
    } else {
        siglongjmp(RESTART, 0xa);
    }
}

void inc() {
    COUNTER += 1;
    CURRENT += CURRENT*2;
    CURRENT += 1;
    siglongjmp(RESTART, 0x15);
}

void dec() {
    COUNTER += 1;
    CURRENT /= 2;
    siglongjmp(RESTART, 0x15);
}
```